fs = require 'fs'
path = require 'path'
changeCase = require 'change-case'
_ = require 'lodash'

module.exports = generateIndexCoffee = (dir)->
  container = changeCase.pascalCase(pickDirName(dir))

  fs.readdir(dir, (err, files)->
    requests = files.map((file)->
      full = path.join(dir, file)

      if fs.statSync(full).isDirectory()
        generateIndexCoffee(full)

      fileName = file.split('.').shift()
      return if fileName == 'index'
      klass = changeCase.pascalCase(fileName)
      name = [container, klass].join('.')
      "#{name} = require './#{fileName}'"
    )

    requests.unshift("module.exports = #{container} = {}")
    requests.push("# Generated by IndexCoffeeGenerator")

    console.log 'generate', indexCoffee = path.join(dir, 'index.coffee')
    fs.writeFileSync(indexCoffee, _.compact(requests).join("\n"));
  )

pickDirName = (dir)->
  dirs = dir.split('/')
  if (last = _.last(dirs)) != ''
    last
  else
    dirs[dirs.length - 2]


if require.main == module
  dir = process.argv[2]
  generateIndexCoffee(dir) if dir?
